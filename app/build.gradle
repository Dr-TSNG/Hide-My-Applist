plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def gitCommitCount = "git rev-list HEAD --count".execute().text.trim()
def gitCommitHash = 'git rev-parse --verify --short HEAD'.execute().text.trim()

android {
    compileSdkVersion 30
    ndkVersion '22.0.7026061'
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.tsng.hidemyapplist"
        versionNameSuffix ".r${gitCommitCount}.${gitCommitHash}"
        minSdkVersion 24
        targetSdkVersion 30
        externalNativeBuild.cmake {
            cppFlags "-std=c++20"
        }

        versionCode 49
        versionName "2.0"
        buildConfigField "int", "SERVICE_VERSION", "49"
        buildConfigField "int", "MIN_RIRU_VERSION", "23"
        buildConfigField "int", "MIN_BACKUP_VERSION", "49"
    }

    signingConfigs {
        release {
            def properties = new Properties()
            def inputStream = project.rootProject.file('local.properties').newDataInputStream()
            properties.load(inputStream)

            def strFile = new File(properties.getProperty('fileDir').toString())
            storeFile file(strFile)
            storePassword properties.getProperty('storePassword')
            keyAlias properties.getProperty('keyAlias')
            keyPassword properties.getProperty('keyPassword')
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "V${variant.versionName}-${variant.buildType.name}.apk"
        }
    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        viewBinding true
    }


    externalNativeBuild.cmake {
        path "src/main/cpp/CMakeLists.txt"
        version "3.10.2"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'https://api.xposed.info/' }
}

dependencies {
    implementation 'com.drakeet.about:about:2.4.1'
    implementation 'com.drakeet.multitype:multitype:4.3.0'
    implementation 'com.scwang.smart:refresh-layout-kernel:2.0.3'
    implementation 'com.scwang.smart:refresh-header-material:2.0.3'
    implementation 'com.github.kyuubiran:EzXHelper:0.3.2'
    implementation "com.github.topjohnwu.libsu:core:3.1.2"

    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.fragment:fragment-ktx:1.4.0-alpha04'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
}